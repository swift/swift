Import(["env", "conf_env"])

if env.get("EXPAT_BUNDLED", False) :
    if env["SCONS_STAGE"] == "flags" :
        env["EXPAT_FLAGS"] = {
                "CPPDEFINES": ["XML_STATIC"],
                "CPPPATH": [Dir("src")],
                "LIBPATH": [Dir(".")],
            }

    if env["SCONS_STAGE"] == "build" :
        myenv = env.Clone()
        myenv.Append(CPPDEFINES = ["XML_STATIC", "HAVE_EXPAT_CONFIG_H"])
        if env.get("BOOST_BUNDLED", False) :
            myenv.Append(CPPPATH = [".", "src", "../Boost/src"])
        else :
            myenv.Append(CPPPATH = [".", "src"])
            myenv.MergeFlags(env["BOOST_FLAGS"])

        conf = Configure(conf_env)
        if conf.CheckFunc('memmove') :
            myenv.Append(CPPDEFINES = ["HAVE_MEMMOVE"])
        conf.Finish()

        env["EXPAT_OBJECTS"] = myenv.SwiftenObject([
                "src/xmltok.c",
                "src/xmlparse.c",
                "src/xmlrole.c"
            ])
